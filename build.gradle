plugins {
  id 'org.springframework.boot' version '2.2.6.RELEASE'
  id 'io.spring.dependency-management' version '1.0.8.RELEASE'
  id 'com.palantir.git-version' version '0.12.3'
  id 'maven-publish'
  id 'java'
  id 'eclipse'
  id 'idea'
}

version gitVersion().replace(".dirty", "")

group = 'com.anothernode'
sourceCompatibility = '1.8'

configurations {
  developmentOnly
  runtimeClasspath {
      extendsFrom developmentOnly
  }
  compileOnly {
      extendsFrom annotationProcessor
  }

  // Needed for publishing.
  // See https://docs.gradle.org/current/userguide/upgrading_version_6.html#publishing_spring_boot_applications
  [apiElements, runtimeElements].each {
      it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
      it.outgoing.artifact(bootJar)
  }
}

sourceSets {
  main {
    java {
      srcDirs 'src/main/java'
      srcDirs 'build/generated/sources/annotationProcessor/java/main'
    }
  }
}

repositories {
  mavenCentral()
}

def autoValueVersion = '1.7'
def assertJVersion = '3.15.0'
def jsoupVersion = '1.13.1'

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
  testImplementation "org.assertj:assertj-core:${assertJVersion}"
  testImplementation "org.jsoup:jsoup:${jsoupVersion}"
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  // Workaround for running tests in VS Code. Can be removed when upstream is fixed.
  // See https://github.com/microsoft/vscode-java-test/issues/971
  // and https://github.com/eclipse/buildship/issues/980
  testImplementation('org.junit.platform:junit-platform-engine:+')
}

test {
  useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/anothernode/game-of-life")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
